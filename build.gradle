plugins {
    id 'java'
    id 'maven'
    id 'net.minecrell.licenser' version '0.3'
}

archivesBaseName = name.toLowerCase()

sourceCompatibility = '1.8'
targetCompatibility = '1.8'

repositories {
    mavenCentral()
}

dependencies {
    compile 'org.apache.logging.log4j:log4j-core:2.8.2'
    compile 'org.jline:jline-terminal:3.2.0'
    compile 'org.jline:jline-reader:3.2.0'

    compileOnly 'com.google.code.findbugs:jsr305:3.0.2'
}

task sourceJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
    archives sourceJar
    archives javadocJar
}

uploadArchives {
    repositories {
        mavenDeployer {
            if (project.hasProperty('releaseRepo')) {
                repository(url: releaseRepo) {
                    if (project.hasProperty('mavenUser')) {
                        authentication(userName: mavenUser, password: mavenPassword)
                    }
                }
            }

            if (project.hasProperty('snapshotRepo')) {
                snapshotRepository(url: snapshotRepo) {
                    if (project.hasProperty('mavenUser')) {
                        authentication(userName: mavenUser, password: mavenPassword)
                    }
                }
            }

            pom {
                artifactId = project.archivesBaseName

                project {
                    name = project.name
                    description = project.description
                    url = project.url

                    scm {
                        url = project.url
                        connection = "scm:git:${project.url}.git"
                        developerConnection = "scm:git:${project.url}.git"
                    }

                    issueManagement {
                        system = 'GitHub Issues'
                        url = "${project.url}/issues"
                    }

                    licenses {
                        license {
                            name = 'MIT License'
                            url = 'http://opensource.org/licenses/MIT'
                            distribution = 'repo'
                        }
                    }
                }
            }
        }
    }
}
